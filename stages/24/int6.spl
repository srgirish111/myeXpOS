//SETTING MODE FLAG FIELD which indicates if execution is in user mode, exception handler or system call.

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 7;

alias userSp R1;
userSp = SP;

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 13] = SP;
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]* 16) + 11] * 512 - 1;

alias physicalPageNum R2;
alias offset R3;
alias fileDescPhysicalAddr R4;

physicalPageNum = [PTBR + 2 * ((userSp - 4)/512)];
offset = (userSp - 4)%512;
fileDescPhysicalAddr = (physicalPageNum * 512) + offset;

alias fileDescriptor R5;
fileDescriptor = [fileDescPhysicalAddr];

alias physicalAddrRetVal R6;
physicalAddrRetVal = ([PTBR + 2* ((userSp - 1) / 512)] * 512) + ((userSp - 1) % 512);

alias word R7;
word = [([PTBR + 2* ((userSp - 3) / 512)] * 512) + ((userSp - 3) % 512)];


if(fileDescriptor != -1)
then
	
	//check if file descriptor is valid
	if(fileDescriptor < 0 || fileDescriptor > 7) then
		[physicalAddrRetVal] = -1;
		SP = userSp;
		[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
		ireturn;
	endif;
	
	alias per_process_entry R8;
	
	per_process_entry = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 11] * 512 + 496 + 2*fileDescriptor;
	

	//check if per-process resource table is valid
	if([per_process_entry] != FILE) then
	
		[physicalAddrRetVal] = -1;
		SP = userSp;
		[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
		ireturn;
	endif;
	
	
	alias open_ft_index R9;
	alias inode_index R10;
	
	open_ft_index = [per_process_entry + 1];
	inode_index = [OPEN_FILE_TABLE + open_ft_index*4];
	
	
	
	//acquire inode
	multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10);
	R1 = 4;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	R2 = inode_index;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10);
	
	//check if inode was acquired
	if(R0 == -1) then
		[physicalAddrRetVal] = -1;
		SP = userSp;
		[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
		ireturn;
	endif;
	
	alias lseek R11;
	lseek = [OPEN_FILE_TABLE + open_ft_index*4 + 2];
	

	
	
	if(inode_index == INODE_ROOT) then
		//lseek is at end of file
		if(lseek >= 480) then
			
			
			//release inode
			multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
			R1 = 5;
			R2 = inode_index;
			R3 = [SYSTEM_STATUS_TABLE + 1];
			call MOD_0;
			multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
			
			[physicalAddrRetVal] = -2;
			SP = userSp;
			[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
			ireturn;
			
		else
			//print("rootval");
			//print [ROOT_FILE + lseek];
			[[[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 14] + 2 * (word/512)] * 512 + (word%512)] = [ROOT_FILE + lseek];
			[OPEN_FILE_TABLE + open_ft_index*4 + 2] = [OPEN_FILE_TABLE + open_ft_index*4 + 2] + 1;
		endif;
	
	else
		//if lseek greater than file size
		if(lseek >= [INODE_TABLE + inode_index*16 + 2]) then
			//release inode
			multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
			R1 = 5;
			R2 =inode_index;
			R3 = [SYSTEM_STATUS_TABLE + 1] ;
			call MOD_0;
			multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
			
			[physicalAddrRetVal] = -2;
			SP = userSp;
			[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
			ireturn;
		endif;
		
		//buffered read
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
		R1 = 2;
		R2 = [SYSTEM_STATUS_TABLE + 1];
		R2 = [INODE_TABLE + inode_index* 16 + 8 + lseek/512];
		R3 = lseek%512;
		R4 = word;
		call MOD_3;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
		
		[OPEN_FILE_TABLE + open_ft_index*4 + 2] = [OPEN_FILE_TABLE + open_ft_index*4 + 2] + 1;
	endif;
	
	
	
	//release inode
	multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
	R1 = 5;
	R2 = inode_index;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
	
	
	
else
	

	multipush(R1,R2,R3,R4,R5,R6,R7);
	//Function number of terminal read
	R1 = 4;
	R2 = [SYSTEM_STATUS_TABLE+1];
	R3 = word;
	call MOD_4;
	multipop(R1,R2,R3,R4,R5,R6,R7);
	
	
endif;

[physicalAddrRetVal] = 0;
SP = userSp;
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;



ireturn;

